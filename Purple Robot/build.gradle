import com.android.builder.core.DefaultManifestParser

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.0'
    }
}

apply plugin: 'com.android.application'

android {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/groups/public/" }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        androidTest.setRoot('tests')
    }

    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 23
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dependencies {
        compile fileTree(dir: 'libs', include: '*.jar')
	    wearApp project(':watch')
        compile 'com.squareup.okhttp:okhttp:2.4.0'
        compile 'com.android.support:appcompat-v7:23.0.1'
        compile 'com.android.support:design:23.0.1'
        compile 'com.android.support:mediarouter-v7:23.0.1'
        compile 'com.google.android.gms:play-services-cast:8.1.0'
        compile 'com.google.android.gms:play-services-fitness:8.1.0'
        compile 'com.google.android.gms:play-services-location:8.1.0'
        compile 'com.google.android.gms:play-services-maps:8.1.0'
        compile 'com.google.android.gms:play-services-wearable:8.1.0'
//        compile 'com.squareup.leakcanary:leakcanary-android:1.4-SNAPSHOT'
        compile 'org.msgpack:msgpack:0.6.12'
        compile 'com.getpebble:pebblekit:2.6.0@aar'
        compile project(':anthracite-clients-android')
        compile project(':xsi-android')
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    signingConfigs {
        release {
            storeFile file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.pro'
            signingConfig signingConfigs.release
            debuggable false

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def file = output.outputFile
                    def manifestParser = new DefaultManifestParser()
                    def String versionName = manifestParser.getVersionName((File) android.sourceSets.main.manifest.srcFile)
                    output.outputFile = new File(file.parent, file.name.replace(".apk", "-" + versionName + ".apk").replace(" ", "-").toLowerCase())
                }
            }
        }
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.pro'
            debuggable true
        }
    }
}

